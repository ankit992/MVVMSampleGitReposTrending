apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'androidx.navigation.safeargs'


android {
    testBuildType "qa"

    compileSdkVersion 28
    defaultConfig {
        applicationId "in.co.ankitarora.mvvmsamplegithubrepos"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "in.co.ankitarora.mvvmsamplegithubrepos.test.CucumberTestRunner"

    }
    buildTypes {
        debug{
            buildConfigField "String", "BASE_URL", '"https://github-trending-api.now.sh/"'
        }
        release {
            buildConfigField "String", "BASE_URL", '"https://github-trending-api.now.sh/"'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        qa {
            initWith debug
            buildConfigField "String", "BASE_URL", '"http://localhost:8080/"'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
        test.java.srcDirs += 'src/test/java'
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
    dataBinding.enabled = true
}

def lifeCycleExtensionVersion = '1.1.1'
def supportVersion = '28.0.0'
def retrofitVersion = '2.3.0'
def glideVersion = '4.9.0'
def rxJavaVersion = '2.1.1'
def navVersion = '2.2.0-rc04'
def daggerVersion = '2.14.1'
def mockitoVersion = '2.11.0'
def cucumberAndroidVersion = '1.2.5'
def cucumberPicoContainerVersion = '1.2.5'
def cucumberJvmDepsVersion = '1.0.5'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation "com.android.support:design:$supportVersion"

    implementation "android.arch.lifecycle:extensions:$lifeCycleExtensionVersion"

    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxJavaVersion"

    implementation "com.github.bumptech.glide:glide:$glideVersion"

    implementation "com.android.support:palette-v7:$supportVersion"

    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0'
    implementation "androidx.test.espresso.idling:idling-net:3.2.0"

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation "android.arch.core:core-testing:1.1.1"
    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:4.2.1"

    androidTestImplementation "info.cukes:cucumber-android:$cucumberAndroidVersion"
    androidTestImplementation "info.cukes:cucumber-picocontainer:$cucumberPicoContainerVersion"
    androidTestImplementation "info.cukes:cucumber-jvm-deps:$cucumberJvmDepsVersion"

    implementation "androidx.test.espresso.idling:idling-concurrent:3.2.0"
    implementation('com.jakewharton.espresso:okhttp3-idling-resource:1.0.0') {
        exclude module: 'support-annotations'
    }
}

gradle.taskGraph.afterTask { task, state ->
    if ((task == connectedAndroidTest) || (task == connectedQaAndroidTest) || (task == connectedCheck)) {
        def reportDir = new File(project.buildDir, "cucumber-reports")
        if (!reportDir.exists()) {
            reportDir.mkdirs()
        }
        println 'Retrieving Cucumber test report to ' + project.buildDir
        def processBuilder = new ProcessBuilder(
                ['adb', 'pull', "/mnt/sdcard/cucumber-reports/", reportDir.getAbsolutePath()])
        processBuilder.start()
    }
}

